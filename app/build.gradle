apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.github.triplet.play'
apply plugin: 'twitter-gradle-plugin'

static def getDate() {
    def date = new Date()

    def formattedDate = date.format('yyMMddHHmm')
    return formattedDate
}

android {
    compileSdkVersion 29

    defaultConfig {
        applicationId "com.sikling.app"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode project.versionCode
        versionName project.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        release {
            storeFile rootProject.file('sikling_android.jks')
            storePassword System.getenv("RELEASE_STORE_PASSWORD")
            keyAlias System.getenv("RELEASE_KEY_ALIAS")
            keyPassword System.getenv("RELEASE_KEY_PASSWORD")
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions "stage"

    productFlavors {
        pro {
            dimension "stage"
        }
        dev {
            dimension "stage"
            versionName "${project.versionName} DEV"
            applicationId "com.sikling.app.dev"
            versionCode Integer.parseInt(getDate())
        }
    }

    variantFilter { variant ->
        def names = variant.flavors*.name
        if ((names.contains("dev") && variant.buildType.name == "release") || (names.contains("pro") &&
                variant.buildType.name ==
                "debug")) {
            variant.ignore = true
        }
    }

    dataBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    def supportLibraryVersion = '1.1.0-alpha02'
    implementation "androidx.appcompat:appcompat:$supportLibraryVersion"
    implementation "com.google.android.material:material:1.1.0-alpha10"
    implementation "androidx.cardview:cardview:1.0.0"

    def butterKnifeVersion = '10.0.0'
    implementation "com.jakewharton:butterknife:$butterKnifeVersion"
    kapt "com.jakewharton:butterknife-compiler:$butterKnifeVersion"

    def glideVersion = '4.8.0'
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    kapt "com.github.bumptech.glide:compiler:$glideVersion"

    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0-alpha02'
}

play {
    jsonFile = file('../sikling_secrets.json')
}

twitterPlugin {
    consumerKey = System.getenv()['TWITTER_API_KEY']
    consumerSecret = System.getenv()['TWITTER_API_SECRET']
    accessToken = System.getenv()['TWITTER_ACCESS_TOKEN']
    accessTokenSecret = System.getenv()['TWITTER_ACCESS_TOKEN_SECRET']
    message = "New Sikling version ${project.versionName} available for download. Check out the details at https://github.com/ferranpons/sikling #Android #application #OpenSource"
}

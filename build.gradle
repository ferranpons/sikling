buildscript {
    ext.kotlin_version = '1.3.11'
    repositories {
        google()
        jcenter()
        maven {
            url  "https://dl.bintray.com/ferranpons/maven"
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.3.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "gradle.plugin.org.jlleitschuh.gradle:ktlint-gradle:4.1.0"
        classpath 'com.github.triplet.gradle:play-publisher:1.2.2'
        classpath 'com.ferranpons:twitter-gradle-plugin:1.1.0'
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "1.0.0.RC6-4"
}

subprojects {
    apply plugin: "org.jlleitschuh.gradle.ktlint" // Version should be inherited from parent
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

detekt {
    version = "1.0.0.RC6-4"
    profile("main") {
        input = "$projectDir/app"
        config = "$projectDir/detekt-config.yml"
        filters = ".*test.*,.*/resources/.*,.*/tmp/.*"
    }
}

ext {
    travisBuild = System.getenv("TRAVIS") == "true"
    versionMajor = getVersionMajor()
    versionMinor = getVersionMinor()
    versionPatch = getVersionPatch()
    versionName = "${versionMajor}.${versionMinor}.${versionPatch}"
    versionCode = versionMajor * 10000 + versionMinor * 100 + versionPatch
}

def getVersionMajor() {
    def content = file('version.properties')

    Properties properties = new Properties()
    InputStream is = new ByteArrayInputStream(content.getBytes())
    properties.load(is)

    return properties['MAJOR'].toInteger()
}

def getVersionMinor() {
    def content = file('version.properties')

    Properties properties = new Properties()
    InputStream is = new ByteArrayInputStream(content.getBytes())
    properties.load(is)

    return properties['MINOR'].toInteger()
}

def getVersionPatch() {
    def content = file('version.properties')

    Properties properties = new Properties()
    InputStream is = new ByteArrayInputStream(content.getBytes())
    properties.load(is)

    return properties['PATCH'].toInteger()
}
